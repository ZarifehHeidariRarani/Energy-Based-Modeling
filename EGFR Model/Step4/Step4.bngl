
#This model is the energy-based model of the kholodenko model 1999(biomodel)
begin model
begin parameters
  #Kholodenko parameters
  #EGF,EGFR Pramaters
  r1_k1f   0.003
  r1_k1b   0.06
  r2_k2f   0.01
  r2_k2b   0.1
  r3_k3f   1.0
  r3_k3b   0.01
  r4_V4    450.0
  r4_K4    50.0
  # Shc Parameters
  r13_k13f 0.09
  r13_k13b 0.6
  r14_k14f 6.0
  r14_k14b 0.06
  r15_k15f 0.3
  r15_k15b 0.0009
  r16_V16  1.7
  r16_K16  340.0
  #Grb2, SOS Parameters
  r9_k9f 0.003
  r9_k9b 0.05
  r10_k10f 0.01
  r10_k10b 0.06
  r11_k11f 0.03
  r11_k11b 0.0045
  r12_k12f 0.0015
  r12_k12b 0.0001
  # Shc Grb2 SOS
  r17_k17f 0.003
  r17_k17b 0.1
  r18_k18f 0.3
  r18_k18b 0.0009
  r19_k19f 0.01
  r19_k19b 0.0214
  r20_k20f 0.12
  r20_k20b 0.00024
  r21_k21f 0.003
  r21_k21b 0.1
  r22_k22f 0.03
  r22_k22b 0.064
  r23_k23f 0.1
  r23_k23b 0.021
  r24_k24f 0.009
  r24_k24b 0.0429
  # PLCg 
  r5_k5f 0.06
  r5_k5b 0.2
  r6_k6f 1.0
  r6_k6b 0.05
  r7_k7f 0.3
  r7_k7b 0.006
  r8_V8 1.0
  r8_K8 100.0
  r25_k25f 1.0
  r25_k25b 0.03
  #Free Energies in STEP1
  Gf_LR   -ln(r1_k1f/r1_k1b)
  Gf_RR   -ln(r2_k2f/r2_k2b)
  Gf_RSh  -ln(r13_k13f/r13_k13b)
  Gf_LRR   10.8505 #  Fixed in step1 (10.8505)
  Gf_LRRL  -15.7 #  Fixed in Step1 (-15.7)
  Gf_RShP  10.3939 # Fixed in step1 (10.3939)
 
  #Free Energies in STEP2
  Gf_RG   -ln(r9_k9f/r9_k9b)
  Gf_GS   -ln(r12_k12b/r12_k12f)
  Gf_RGS   0.35#0.8482 #-2.8482 
  #Free energies in step3
  Gf_ShG   -ln(r21_k21f/r21_k21b)
  #Gf_ShG   -ln(r17_k17f/r17_k17b)
  Gf_RShG   -10.3
  Gf_ShGS   10
  #Gf_RShGS  1.5
 #Free energies in STEP 4_1
  Gf_RPL  -ln(r5_k5f/r5_k5b)

 #Free energies in STEP 4_2
  Gf_RPLP  1.9 #2
  #rate distribution parameter, no units
  phi_LR   0.5
  phi_RR   0.01
  phi_RSh  0.1
  phi_RG   0.001
  phi_GS   0.75
  phi_ShG  0.5
  phi_RPL  0.1
  #Activation Energies
  Ea_LR      -(phi_LR*ln(r1_k1b)+(1-phi_LR)*ln(r1_k1f)) 
  Ea_RR      -(phi_RR*ln(r2_k2b)+(1-phi_RR)*ln(r2_k2f)) 
  Ea_RSh     -(phi_RSh*ln(r13_k13b)+(1-phi_RSh)*ln(r13_k13f)) 
  Ea_RG      -(phi_RG*ln(r9_k9b)+(1-phi_RG)*ln(r9_k9f))
  Ea_GS      -(phi_GS*ln(r12_k12f)+(1-phi_GS)*ln(r12_k12b))
  Ea_ShG     -(phi_ShG*ln(r21_k21b)+(1-phi_ShG)*ln(r21_k21f))
  Ea_RPL     -(phi_RPL*ln(r5_k5b)+(1-phi_RPL)*ln(r5_k5f))
end parameters

begin compartments
end compartments

begin molecule types
  EGF(egfr)
  EGFR(egf,egfr,p~P~0,shc,grb2,plcg)
  Shc(egfr,p~P~0,grb2)
  Grb2(egfr,sos,shc) 
  SOS(grb2) 
  PLCg(egfr,p~0~P,p_i~0~I)
end molecule types

begin seed species
  EGF(egfr) 680 #EGF nM
  EGFR(egf,egfr,p~0,shc,grb2,plcg) 100.0 #EGFR nM
  Shc(egfr,p~0,grb2) 150.0 #Shc nM
  Grb2(egfr,sos,shc) 85.0 #Grb2 nM
  SOS(grb2) 34.0 #SOS nM
  PLCg(egfr,p~0,p_i~0) 105.0 #nM
end seed species



begin energy patterns
   # bond energy
  EGFR(egf!0).EGF(egfr!0)                                        Gf_LR
  EGFR(egfr!1).EGFR(egfr!1)                                      Gf_RR+ 2*Gf_LRR + Gf_LRRL
  EGFR(p~P,shc!2).Shc(egfr!2)                                    Gf_RSh
  EGFR(p~P,shc!2).Shc(egfr!2,p~P)                                Gf_RShP
  EGFR(p~P,grb2!3).Grb2(egfr!3)                                  Gf_RG
  Grb2(sos!4).SOS(grb2!4)                                        Gf_GS
  EGFR(p~P,grb2!3).Grb2(egfr!3,sos!4).SOS(grb2!4)                Gf_RGS
  Shc(p~P,grb2!0).Grb2(shc!0)                                    Gf_ShG
  EGFR(p~P,shc!2).Shc(egfr!2,p~P,grb2!3).Grb2(shc!3)             Gf_RShG
  Shc(p~P,grb2!2).Grb2(shc!2,sos!3).SOS(grb2!3)                  Gf_ShGS
  EGFR(p~P,plcg!2).PLCg(egfr!2)                                  Gf_RPL
  EGFR(p~P,plcg!2).PLCg(egfr!2,p~P)                              Gf_RPLP
end energy patterns

begin observables
  
  Species EGF_EGFR2_P          EGFR(egfr,egf!1,p~P).EGF(egfr!1)    #RP in Kholodenko's paper
  
  Species Shc_P                Shc(egfr,p~P,grb2) #ShP in Kholodenko's paper
  
  Molecules EGFRPtotal EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1), EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1), \
                       EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~0,grb2).EGF(egfr!1), EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~0,grb2).EGF(egfr!1), \
                       EGFR(egfr,egf!1,p~0,shc!2,grb2,plcg).Shc(egfr!2,p~P,grb2).EGF(egfr!1), EGFR(egfr,egf!1,p~0,shc!2,grb2,plcg).Shc(egfr!2,p~P,grb2).EGF(egfr!1), \
                       EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2,shc,sos).EGF(egfr!1), EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2,sos,shc).EGF(egfr!1), \
                       EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2,sos!3).EGF(egfr!1).SOS(grb2!3), EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2,sos!3).EGF(egfr!1).SOS(grb2!3), \
                       EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2,sos,shc!4).EGF(egfr!1).Shc(egfr,p~P,grb2!4), EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2,sos,shc!4).EGF(egfr!1).Shc(egfr,p~P,grb2!4), \
                       EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2,sos!3,shc!4).EGF(egfr!1).Shc(egfr,p~P,grb2!4).SOS(grb2!3), EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2,sos!3,shc!4).EGF(egfr!1).Shc(egfr,p~P,grb2!4).SOS(grb2!3), \
                       EGFR(egfr,egf!1,grb2,p~P,plcg!2,shc).PLCg(egfr!2,p~?,p_i~0).EGF(egfr!1), EGFR(egfr,egf!1,grb2,p~P,plcg!2,shc).PLCg(egfr!2,p~?,p_i~0).EGF(egfr!1),

  Molecules ShPtotal       Shc(egfr!?,p~P,grb2!?)
  Molecules Grb2EGFRtotal  EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2,sos,shc).EGF(egfr!1), \
                           EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2,sos!3).EGF(egfr!1).SOS(grb2!3), \
                           EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2,sos,shc!4).EGF(egfr!1).Shc(egfr,p~P,grb2!4), \
                           EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2,sos!3,shc!4).EGF(egfr!1).Shc(egfr,p~P,grb2!4).SOS(grb2!3),
                
  Molecules Grb2Shctotal    Shc(egfr!?,grb2!1,p~P).Grb2(shc!1,sos!?),
  Molecules PLCg_P          PLCg(egfr,p~P,p_i~0)
  Molecules  PLCgPtotal     PLCg(egfr,p~P,p_i~0),  EGFR(egfr,egf!1,grb2,p~P,plcg!2,shc).PLCg(egfr!2,p~P,p_i~0).EGF(egfr!1)         

end observables 

begin functions                                              
  fRate3() = r4_V4 /(EGF_EGFR2_P +r4_K4)
  fRate15() = r16_V16 /(Shc_P+r16_K16)
  fRate7() = r8_V8/(PLCg_P +r8_K8)
end functions

begin reaction rules
    # Ligand-receptor binding 
  v1: EGFR(egfr,egf,p~0,shc,grb2,plcg) + EGF(egfr) <-> EGFR(egfr,egf!1,p~0,shc,grb2,plcg).EGF(egfr!1) Arrhenius(phi_LR,Ea_LR) #r1_k1f,r1_k1b
    # Receptor-aggregation
  v2: EGFR(egfr,egf!1,p~0,shc,grb2,plcg).EGF(egfr!1) + EGFR(egfr,egf!1,p~0,shc,grb2,plcg).EGF(egfr!1) <-> EGFR(egfr!1,egf,p~0,shc,grb2,plcg).EGFR(egfr!1,egf,p~0,shc,grb2,plcg) Arrhenius(phi_RR,Ea_RR)
    # Transphosphorylation of Egfr
  v3: EGFR(egfr!1,egf,p~0,shc,grb2,plcg).EGFR(egfr!1,egf,p~0,shc,grb2,plcg) <-> EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) r3_k3f,r3_k3b #Arrhenius(phi_Rp,Ea_Rp) #
    # Dephosphorylayion by phosphotase
  v4: EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) -> EGFR(egfr!1,egf,p~0,shc,grb2,plcg).EGFR(egfr!1,egf,p~0,shc,grb2,plcg) fRate3
    # Egfr + Shc
  v13: Shc(egfr) + EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2).EGF(egfr!1) Arrhenius(phi_RSh,Ea_RSh) #r13_k13f,r13_k13b
    # Shc transphos
  v14: EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~0).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~0,shc!2,grb2,plcg).Shc(egfr!2,p~P).EGF(egfr!1)  r14_k14f,r14_k14b 
  #   RShcP phosphorylation break apart 
  v15: EGFR(egfr,egf!1,p~0,shc!2,grb2,plcg).Shc(egfr!2,p~P).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) + Shc(egfr,p~P) r15_k15f,r15_k15b
   #  Dephosphorylayion of Shc by phosphotase
  v16: Shc(egfr,p~P) -> Shc(egfr,p~0) fRate15
  #   EGFR + Grb2
  v9:  Grb2(egfr) + EGFR(egfr,egf!1,grb2,p~P,shc,plcg).EGF(egfr!1) <-> EGFR(egfr,egf!1,grb2!2,p~P,shc,plcg).Grb2(egfr!2).EGF(egfr!1)  Arrhenius(phi_RG,Ea_RG)#r9_k9f,r9_k9b
  #   Grb2 + SOS
  v12: Grb2(egfr!+,sos!1).SOS(grb2!1) <-> Grb2(egfr!+,sos) + SOS(grb2) Arrhenius(phi_GS,Ea_GS) #r12_k12f,r12_k12b
  #   Grb2 + Shc
  v21: Grb2(egfr,shc) + Shc(egfr,grb2,p~P) <-> Shc(egfr,grb2!1,p~P).Grb2(egfr,shc!1) Arrhenius(phi_ShG,Ea_ShG) #r21_k21f,r21_k21b
 
 # EGFR + PLCg
  v5: EGFR(egfr,egf!1,grb2,p~P,plcg,shc).EGF(egfr!1) + PLCg(egfr,p_i~0,p~0) <-> EGFR(egfr,egf!1,grb2,p~P,plcg!2,shc).PLCg(egfr!2,p_i~0,p~0).EGF(egfr!1) Arrhenius(phi_RPL,Ea_RPL)#r5_k5f,r5_k5b

 # PLCg transphos
  EGFR(egfr,egf!1,grb2,p~P,plcg!2,shc).PLCg(egfr!2,p~0,p_i~0).EGF(egfr!1) <-> EGFR(egfr,egf!1,grb2,p~P,plcg!2,shc).PLCg(egfr!2,p~P,p_i~0).EGF(egfr!1)  r6_k6f,r6_k6b
 
 #RPLCP phosphorylation break apart
 EGFR(egfr,egf!1,grb2,p~P,plcg!2,shc).PLCg(egfr!2,p~P,p_i~0).EGF(egfr!1) <-> EGFR(egfr,egf!1,grb2,p~P,plcg,shc).EGF(egfr!1)+PLCg(egfr,p~P,p_i~0)  r7_k7f, r7_k7b
 # PLCg dephosphorylation, catalytic
  PLCg(egfr,p~P,p_i~0) -> PLCg(egfr,p~0,p_i~0)   fRate7

 v25: PLCg(egfr,p~P,p_i~0) <-> PLCg(egfr,p~0,p_i~I) r25_k25f,r25_k25b

end reaction rules

end model

generate_network({overwrite=>1,TextReaction=>0,print_iter=>0,max_iter=>99})
simulate({method=>"ode",t_end=>120,n_steps=>120})



