#This model is the energy-based model of the kholodenko model 1999(biomodel)
begin model
begin parameters
  #Kholodenko parameters
  #EGF,EGFR Pramaters
  r3_k3f   1.0
  r3_k3b   0.01
  r4_V4    450.0
  r4_K4    50.0
  # Shc Parameters
  r14_k14f 6.0
  r14_k14b 0.06
  r15_k15f 0.3
  r15_k15b 0.0009
  r16_V16  1.7
  r16_K16  340.0
  #Free Energies in STEP1
  Gf_LR    2.9957
  Gf_RR    2.3026
  Gf_RSh   1.8971
  Gf_LRR   10.8505   #Fixed in step1 
  Gf_LRRL  -15.7     #Fixed in Step1 
  Gf_RShP  10.3939   #Fixed in step1 
  #Free Energies in STEP2
  Gf_RG    2.8134    #Fixed in Step2
  Gf_GS    2.7081    #Fixed in Step2
  Gf_RGS   0.35      #Fixed in Step2
  #Free Energies in STEP3
  Gf_ShG   3.5066
  Gf_RShG  -10.3
  Gf_ShGS  10
  #rate distribution parameter, no units
  phi      0.5
  #Activation Energies
  Ea_LR    4.3113
  Ea_RR    3.4539
  Ea_RSh   1.4594
  Ea_RG    4.4024
  Ea_GS    7.8563
  Ea_ShG   4.0559
end parameters

begin compartments
end compartments

begin molecule types
  EGF(egfr)
  EGFR(egf,egfr,p~P~0,shc,grb2)
  Shc(egfr,p~P~0,grb2)
  Grb2(egfr,sos,shc) 
  SOS(grb2) 
end molecule types

begin seed species
  EGF(egfr)                     680   #nM
  EGFR(egf,egfr,p~0,shc,grb2)   100.0 #nM
  Shc(egfr,p~0,grb2)            150.0 #nM
  Grb2(egfr,sos,shc)            85.0  #nM
  SOS(grb2)                     34.0  #nM
end seed species



begin energy patterns
  EGFR(egf!0).EGF(egfr!0)                                        Gf_LR
  EGFR(egfr!1).EGFR(egfr!1)                                      Gf_RR+ 2*Gf_LRR + Gf_LRRL
  EGFR(p~P,shc!2).Shc(egfr!2)                                    Gf_RSh
  EGFR(p~P,shc!2).Shc(egfr!2,p~P)                                Gf_RShP
  EGFR(p~P,grb2!3).Grb2(egfr!3)                                  Gf_RG
  Grb2(sos!4).SOS(grb2!4)                                        Gf_GS
  EGFR(p~P,grb2!3).Grb2(egfr!3,sos!4).SOS(grb2!4)                Gf_RGS
  Shc(p~P,grb2!0).Grb2(shc!0)                                    Gf_ShG
  EGFR(p~P,shc!2).Shc(egfr!2,p~P,grb2!3).Grb2(shc!3)             Gf_RShG
  Shc(p~P,grb2!2).Grb2(shc!2,sos!3).SOS(grb2!3)                  Gf_ShGS
end energy patterns

begin observables
  Species EGF_EGFR2_P      EGFR(egfr,egf!1,p~P).EGF(egfr!1)    #RP in Kholodenko's paper
  Species Shc_P            Shc(egfr,p~P,grb2)                  #ShP in Kholodenko's paper
  Molecules EGFRPtotal     EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1), EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1), \
                           EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~0,grb2).EGF(egfr!1), EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~0,grb2).EGF(egfr!1), \
                           EGFR(egfr,egf!1,p~0,shc!2,grb2).Shc(egfr!2,p~P,grb2).EGF(egfr!1), EGFR(egfr,egf!1,p~0,shc!2,grb2).Shc(egfr!2,p~P,grb2).EGF(egfr!1), \
                           EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2,shc,sos).EGF(egfr!1), EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2,sos,shc).EGF(egfr!1), \
                           EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2,sos!3).EGF(egfr!1).SOS(grb2!3), EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2,sos!3).EGF(egfr!1).SOS(grb2!3), \
                           EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2,sos,shc!4).EGF(egfr!1).Shc(egfr,p~P,grb2!4), EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2,sos,shc!4).EGF(egfr!1).Shc(egfr,p~P,grb2!4),\
                           EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2,sos!3,shc!4).EGF(egfr!1).Shc(egfr,p~P,grb2!4).SOS(grb2!3), EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2,sos!3,shc!4).EGF(egfr!1).Shc(egfr,p~P,grb2!4).SOS(grb2!3),
  Molecules ShPtotal1      Shc(egfr!?,p~P,grb2!?)
  Molecules Grb2EGFRtotal  EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2,sos,shc).EGF(egfr!1), \
                           EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2,sos!3).EGF(egfr!1).SOS(grb2!3), \
                           EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2,sos,shc!4).EGF(egfr!1).Shc(egfr,p~P,grb2!4), \
                           EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2,sos!3,shc!4).EGF(egfr!1).Shc(egfr,p~P,grb2!4).SOS(grb2!3),             
  Molecules Grb2Shctotal   Shc(egfr!?,grb2!1,p~P).Grb2(shc!1,sos!?),
end observables 

begin functions                                              
  fRate3()  =  r4_V4 /(EGF_EGFR2_P +r4_K4)
  fRate15() =  r16_V16 /(Shc_P+r16_K16)
end functions

begin reaction rules
    # Ligand-receptor binding 
  v1: EGFR(egfr,egf,p~0,shc,grb2) + EGF(egfr) <-> EGFR(egfr,egf!1,p~0,shc,grb2).EGF(egfr!1) Arrhenius(phi,Ea_LR) 
    # Receptor-aggregation
  v2: EGFR(egfr,egf!1,p~0,shc,grb2).EGF(egfr!1) + EGFR(egfr,egf!1,p~0,shc,grb2).EGF(egfr!1) <-> EGFR(egfr!1,egf,p~0,shc,grb2).EGFR(egfr!1,egf,p~0,shc,grb2) Arrhenius(phi,Ea_RR)
    # Transphosphorylation of Egfr
  v3: EGFR(egfr!1,egf,p~0,shc,grb2).EGFR(egfr!1,egf,p~0,shc,grb2) <-> EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) r3_k3f,r3_k3b 
    # Dephosphorylayion by phosphotase
  v4: EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) -> EGFR(egfr!1,egf,p~0,shc,grb2).EGFR(egfr!1,egf,p~0,shc,grb2) fRate3
    # Egfr + Shc
  v13: Shc(egfr) + EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2).EGF(egfr!1) Arrhenius(phi,Ea_RSh) 
    # Shc transphos
  v14: EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~0).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~0,shc!2,grb2).Shc(egfr!2,p~P).EGF(egfr!1)  r14_k14f,r14_k14b 
    # RShcP phosphorylation break apart 
  v15: EGFR(egfr,egf!1,p~0,shc!2,grb2).Shc(egfr!2,p~P).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) + Shc(egfr,p~P) r15_k15f,r15_k15b
    # Dephosphorylayion of Shc by phosphotase
  v16: Shc(egfr,p~P) -> Shc(egfr,p~0) fRate15
    # EGFR + Grb2
  v9:  Grb2(egfr) + EGFR(egfr,egf!1,grb2,p~P,shc).EGF(egfr!1) <-> EGFR(egfr,egf!1,grb2!2,p~P,shc).Grb2(egfr!2).EGF(egfr!1)  Arrhenius(phi,Ea_RG)
    # Grb2 + SOS
  v12: Grb2(egfr!+,sos!1).SOS(grb2!1) <-> Grb2(egfr!+,sos) + SOS(grb2) Arrhenius(phi,Ea_GS) 
    # Grb2 + Shc
  v21: Grb2(egfr,shc) + Shc(egfr,grb2,p~P) <-> Shc(egfr,grb2!1,p~P).Grb2(egfr,shc!1) Arrhenius(phi,Ea_ShG) 

end reaction rules

end model

generate_network({overwrite=>1,TextReaction=>0,print_iter=>0,max_iter=>99})
simulate({method=>"ode",t_end=>120,n_steps=>120})



