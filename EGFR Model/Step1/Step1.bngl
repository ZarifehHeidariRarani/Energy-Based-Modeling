
#This model is the energy-based model of the kholodenko model 1999(biomodel)
begin model
begin parameters
  #Kholodenko parameters
  #EGF,EGFR Pramaters
  r1_k1f   0.003
  r1_k1b   0.06
  r2_k2f   0.01
  r2_k2b   0.1
  r3_k3f   1.0
  r3_k3b   0.01
  r4_V4    450.0
  r4_K4    50.0
  # Shc Parameters
  r13_k13f 0.09
  r13_k13b 0.6
  r14_k14f 6.0
  r14_k14b 0.06
  r15_k15f 0.3
  r15_k15b 0.0009
  r16_V16  1.7
  r16_K16  340.0

  #Free Energies
  Gf_LR   -ln(r1_k1f/r1_k1b)
  Gf_RR   -ln(r2_k2f/r2_k2b)
  Gf_RSh  -ln(r13_k13f/r13_k13b)
  Gf_LRR   10.8505 
  Gf_LRRL  -15.7
  Gf_RShP   10.3939 

  #rate distribution parameter, no units
  phi_LR   0.5
  phi_RR   0.01
  phi_RSh  0.1

  #Activation Energies
  Ea_LR      -(phi_LR*ln(r1_k1b)+(1-phi_LR)*ln(r1_k1f)) 
  Ea_RR      -(phi_RR*ln(r2_k2b)+(1-phi_RR)*ln(r2_k2f)) 
  Ea_RSh     -(phi_RSh*ln(r13_k13b)+(1-phi_RSh)*ln(r13_k13f)) 

end parameters

begin compartments
end compartments

begin molecule types
  EGF(egfr)
  EGFR(egf,egfr,p~P~0,shc,grb2)
  Shc(egfr,p~P~0)
end molecule types

begin seed species
  EGF(egfr) 680 #EGF nM
  EGFR(egf,egfr,p~0,shc,grb2) 100.0 #EGFR nM
  Shc(egfr,p~0) 150.0 #Shc nM
end seed species

begin energy patterns
   # bond energy
  EGFR(egf!0).EGF(egfr!0)                                        Gf_LR
  EGFR(egfr!1).EGFR(egfr!1)                                      Gf_RR+ 2*Gf_LRR + Gf_LRRL
  EGFR(p~P,shc!2).Shc(egfr!2)                                    Gf_RSh
  EGFR(p~P,shc!2).Shc(egfr!2,p~P)                                Gf_RShP
end energy patterns

begin observables
 
 Molecules EGF_EGFR2_P          EGFR(egfr,egf!1,p~P,shc).EGF(egfr!1)    
 
 Molecules Shc_P                Shc(egfr,p~P) 
  
 Molecules EGFRPtotal  EGFR(egfr,egf!1,p~P,shc).EGF(egfr!1), EGFR(egfr,egf!1,p~P,shc).EGF(egfr!1), \
                       EGFR(egfr,egf!1,p~P,shc!2).Shc(egfr!2,p~0).EGF(egfr!1), EGFR(egfr,egf!1,p~P,shc!2).Shc(egfr!2,p~0).EGF(egfr!1), \
                       EGFR(egfr,egf!1,p~0,shc!2).Shc(egfr!2,p~P).EGF(egfr!1), EGFR(egfr,egf!1,p~0,shc!2).Shc(egfr!2,p~P).EGF(egfr!1)

 Molecules ShPtotal   EGFR(egfr,egf!1,p~P,shc!2).Shc(egfr!2,p~P).EGF(egfr!1), \
                       Shc(egfr,p~P), 
                  
end observables 

begin functions                                              
  fRate3() = r4_V4 /(EGF_EGFR2_P +r4_K4)
  fRate15() = r16_V16 /(Shc_P+r16_K16)
end functions

begin reaction rules
    # Ligand-receptor binding 
  v1: EGFR(egfr,egf,p~0,shc,grb2) + EGF(egfr) <-> EGFR(egfr,egf!1,p~0,shc,grb2).EGF(egfr!1) Arrhenius(phi_LR,Ea_LR) #r1_k1f,r1_k1b
    # Receptor-aggregation
  v2: EGFR(egfr,egf!1,p~0,shc,grb2).EGF(egfr!1) + EGFR(egfr,egf!1,p~0,shc,grb2).EGF(egfr!1) <-> EGFR(egfr!1,egf,p~0,shc,grb2).EGFR(egfr!1,egf,p~0,shc,grb2) Arrhenius(phi_RR,Ea_RR)
    # Transphosphorylation of Egfr
  v3: EGFR(egfr!1,egf,p~0,shc,grb2).EGFR(egfr!1,egf,p~0,shc,grb2) <-> EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) r3_k3f,r3_k3b #Arrhenius(phi_Rp,Ea_Rp) #
    # Dephosphorylayion by phosphotase
  v4: EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) -> EGFR(egfr!1,egf,p~0,shc,grb2).EGFR(egfr!1,egf,p~0,shc,grb2) fRate3
    # Egfr + Shc
  v13: Shc(egfr) + EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2).EGF(egfr!1) Arrhenius(phi_RSh,Ea_RSh) #r13_k13f,r13_k13b
    # Shc transphos
  v14: EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~0).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~0,shc!2,grb2).Shc(egfr!2,p~P).EGF(egfr!1)  r14_k14f,r14_k14b 
  #   RShcP phosphorylation break apart 
  v15: EGFR(egfr,egf!1,p~0,shc!2,grb2).Shc(egfr!2,p~P).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) + Shc(egfr,p~P) r15_k15f,r15_k15b
   #  Dephosphorylayion of Shc by phosphotase
  v16: Shc(egfr,p~P) -> Shc(egfr,p~0) fRate15
end reaction rules

end model

generate_network({overwrite=>1,TextReaction=>0,print_iter=>0,max_iter=>99})
simulate({method=>"ode",t_end=>120,n_steps=>120})



