#This model is inferred from kholodenko model (biomodel)
begin model
begin parameters
  r1_k1f   0.003
  r1_k1b   0.06
  r2_k2f   0.01
  r2_k2b   0.1
  r3_k3f   1.0
  r3_k3b   0.01
  r4_V4    450.0
  r4_K4    50.0
  r9_k9f   0.003
  r9_k9b   0.05
  r10_k10f 0.01
  r10_k10b 0.06
  r11_k11f 0.03
  r11_k11b 0.0045
  r12_k12f 0.0015
  r12_k12b 0.0001
  r13_k13f 0.09
  r13_k13b 0.6
  r14_k14f 6.0
  r14_k14b 0.06
  r15_k15f 0.3
  r15_k15b 0.0009
  r16_V16  1.7
  r16_K16  340.0
  r17_k17f 0.003
  r17_k17b 0.1
  r18_k18f 0.3
  r18_k18b 0.0009
  r19_k19f 0.01
  r19_k19b 0.0214
  r20_k20f 0.12
  r20_k20b 0.00024
  r21_k21f 0.003
  r21_k21b 0.1
  r22_k22f 0.03
  r22_k22b 0.064
  r23_k23f 0.1
  r23_k23b 0.021
  r24_k24f 0.009
  r24_k24b 0.0429
end parameters

begin molecule types
  EGF(egfr)
  EGFR(egf,egfr,p~P~0,shc,grb2)
  Shc(egfr,p~P~0,grb2)
  Grb2(egfr,sos,shc)
  SOS(grb2)
end molecule types

begin seed species
  EGF(egfr)                   680 #nM
  EGFR(egf,egfr,p~0,shc,grb2) 100.0 #nM
  Shc(egfr,p~0,grb2)          150.0 #nM
  Grb2(egfr,sos,shc)          85 #nM
  SOS(grb2)                   34 #nM
end seed species

begin observables
  Species EGF_EGFR2_P     EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1)    #RP in Kholodenko's paper
  Species Shc_P           Shc(egfr,p~P,grb2)                                #ShP in Kholodenko's paper
  Molecules EGFRPtotal    EGFR(p~P),EGFR(p~P)
  Molecules ShPtotal      Shc(p~P) 
  Molecules Grb2EGFRtotal Grb2(egfr!+), EGFR(shc!1).Shc(egfr!1,grb2!2).Grb2(shc!2)     
  Molecules Grb2Shctotal  Grb2(shc!+)    
end observables   

begin functions                                              
  fRate3() =  r4_V4/(EGF_EGFR2_P + r4_K4)
  fRate15() = r16_V16/(Shc_P + r16_K16)
end functions

begin reaction rules
 # Ligand binding
  v1:  EGFR(egfr,egf,p~0,shc,grb2) + EGF(egfr) <-> EGFR(egfr,egf!1,p~0,shc,grb2).EGF(egfr!1) r1_k1f,r1_k1b
 # Dimerization
  v2:  EGFR(egfr,egf!1,p~0,shc,grb2).EGF(egfr!1) + EGFR(egfr,egf!1,p~0,shc,grb2).EGF(egfr!1) <-> EGFR(egfr!1,egf,p~0,shc,grb2).EGFR(egfr!1,egf,p~0,shc,grb2) 2*r2_k2f,0.5*r2_k2b
 # Phosphorylation
  v3:  EGFR(egfr!1,egf,p~0,shc,grb2).EGFR(egfr!1,egf,p~0,shc,grb2) <-> EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) 0.5*r3_k3f,r3_k3b
 # Dephosphorylation
  v4:  EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) -> EGFR(egfr!1,egf,p~0,shc,grb2).EGFR(egfr!1,egf,p~0,shc,grb2) fRate3
    # EGFR + Grb2
  v9:  Grb2(egfr,sos,shc) + EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc,grb2!2).Grb2(egfr!2,sos,shc).EGF(egfr!1)  r9_k9f,r9_k9b
  # EGFRGrb2 + SOS
  v10: EGFR(egfr,egf!1,p~P,shc,grb2!2).Grb2(egfr!2,sos,shc).EGF(egfr!1) + SOS(grb2) <-> EGFR(egfr,egf!1,p~P,shc,grb2!2).Grb2(egfr!2,sos!3,shc).EGF(egfr!1).SOS(grb2!3)  r10_k10f,r10_k10b
  v11: EGFR(egfr,egf!1,p~P,shc,grb2!2).Grb2(egfr!2,sos!3,shc).EGF(egfr!1).SOS(grb2!3) <-> EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) + Grb2(egfr,sos!3,shc).SOS(grb2!3)  r11_k11f,r11_k11b
  # SOS + Grb2
  v12: Grb2(egfr,sos!3,shc).SOS(grb2!3) <-> Grb2(egfr,sos,shc) + SOS(grb2)  r12_k12f,r12_k12b
  # EGFR + Shc
  v13: Shc(egfr,p~0,grb2) + EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~0,grb2).EGF(egfr!1) r13_k13f,r13_k13b
  # Shc Phosphorylation
  v14: EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~0,grb2).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~P,grb2).EGF(egfr!1) r14_k14f,r14_k14b
  # RShcP Breaking apart
  v15: EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~P,grb2).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) + Shc(egfr,p~P,grb2) r15_k15f,r15_k15b
  # Shc Dephosphorylation
  v16: Shc(egfr,p~P,grb2) -> Shc(egfr,p~0,grb2) fRate15
  # RShcP + Grb2
  v17: EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~P,grb2).EGF(egfr!1) + Grb2(egfr,sos,shc) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~P,grb2!3).EGF(egfr!1).Grb2(egfr,sos,shc!3)  r17_k17f, r17_k17b
  # RShG Breaking apart
  v18: EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~P,grb2!3).EGF(egfr!1).Grb2(egfr,sos,shc!3) <-> EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) + Shc(egfr,p~P,grb2!3).Grb2(egfr,sos,shc!3)  r18_k18f,r18_k18b
  # RShG + SOS
  v19: EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~P,grb2!3).EGF(egfr!1).Grb2(egfr,sos,shc!3) + SOS(grb2) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~P,grb2!3).EGF(egfr!1).Grb2(egfr,sos!4,shc!3).SOS(grb2!4)  r19_k19f,r19_k19b
  # RShGS Breaking apart
  v20: EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~P,grb2!3).EGF(egfr!1).Grb2(egfr,sos!4,shc!3).SOS(grb2!4) <-> EGFR(egfr,egf!1,p~P,shc,grb2).EGF(egfr!1) + Shc(egfr,p~P,grb2!3).Grb2(egfr,sos!4,shc!3).SOS(grb2!4)   r20_k20f,r20_k20b
  # ShcP + Grb2
  v21: Shc(egfr,p~P,grb2) + Grb2(egfr,sos,shc) <-> Shc(egfr,p~P,grb2!1).Grb2(egfr,sos,shc!1)    r21_k21f,r21_k21b
  # ShG + SOS
  v22: Shc(egfr,p~P,grb2!1).Grb2(egfr,sos,shc!1) + SOS(grb2) <-> Shc(egfr,p~P,grb2!1).Grb2(egfr,sos!2,shc!1).SOS(grb2!2)  r22_k22f,r22_k22b
  # ShGS Breaking apart
  v23: Shc(egfr,p~P,grb2!1).Grb2(egfr,sos!2,shc!1).SOS(grb2!2) <-> Shc(egfr,p~P,grb2) + Grb2(egfr,sos!2,shc).SOS(grb2!2)   r23_k23f,r23_k23b
  # RShP + GS
  v24: EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~P,grb2).EGF(egfr!1) + Grb2(egfr,sos!4,shc).SOS(grb2!4) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2).Shc(egfr!2,p~P,grb2!3).EGF(egfr!1).Grb2(egfr,sos!4,shc!3).SOS(grb2!4)   r24_k24f,r24_k24b
end reaction rules

end model

simulate({method=>"ode",t_end=>120,n_steps=>120})
