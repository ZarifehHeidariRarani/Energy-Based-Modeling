#This model is inferred from kholodenko model (biomodel)
begin model
begin parameters
  r1_k1f   0.003
  r1_k1b   0.06
  r2_k2f   0.01
  r2_k2b   0.1
  r3_k3f   1.0
  r3_k3b   0.01
  r4_V4    450.0
  r4_K4    50.0
  r9_k9f   0.003
  r9_k9b   0.05
  r10_k10f 0.01
  r10_k10b 0.06
  r11_k11f 0.03
  r11_k11b 0.0045
  r12_k12f 0.0015
  r12_k12b 0.0001
  r13_k13f 0.09
  r13_k13b 0.6
  r14_k14f 6.0
  r14_k14b 0.06
  r15_k15f 0.3
  r15_k15b 0.0009
  r16_V16  1.7
  r16_K16  340.0
  r17_k17f 0.003
  r17_k17b 0.1
  r18_k18f 0.3
  r18_k18b 0.0009
  r19_k19f 0.01
  r19_k19b 0.0214
  r20_k20f 0.12
  r20_k20b 0.00024
  r21_k21f 0.003
  r21_k21b 0.1
  r22_k22f 0.03
  r22_k22b 0.064
  r23_k23f 0.1
  r23_k23b 0.021
  r24_k24f 0.009
  r24_k24b 0.0429
end parameters

begin seed species
  EGF()       680 #nM
  EGFR()      100.0 #nM
  Shc()       150.0 #nM
  Grb2()      85
  SOS()       34
end seed species

begin observables
  #Molecules EGF EGF()
  #Molecules EGFR EGFR()
  #Molecules Shc Shc()
  Species EGF_EGFR2_P     RP()    #RP in Kholodenko's paper
  Species Shc_P           ShP()   #ShP in Kholodenko's paper
  Molecules EGFRPtotal    RP(),RP(),R_Sh(),R_Sh(),R_ShP(),R_ShP(), R_G(), R_G(), R_GS(), R_GS(), R_Sh_G(), R_Sh_G(), R_Sh_GS(), R_Sh_GS()                      
  Molecules ShPtotal      ShP(),R_ShP(), R_Sh_G(), R_Sh_GS(),Sh_G(), Sh_GS() 
  Molecules Grb2EGFRtotal R_G(), R_GS(), R_Sh_G(), R_Sh_GS()
  Molecules Grb2Shctotal  R_Sh_G(), R_Sh_GS(), Sh_G(), Sh_GS()
end observables   

begin functions                                              
  fRate3() =  r4_V4 /(EGF_EGFR2_P +r4_K4)
  fRate15() = r16_V16 /(Shc_P+r16_K16)
end functions

begin reaction rules
  # Ligand binding
  v1:  EGF() + EGFR() <-> Ra() r1_k1f,r1_k1b
  # Dimerization
  v2:  Ra() + Ra() <-> R2() 2*r2_k2f,r2_k2b
  # Phosphorylation
  v3:  R2() <-> RP() r3_k3f,r3_k3b
  # Dephosphorylation
  v4:  RP() -> R2() fRate3
  # EGFR + Grb2
  v9:  Grb2() + RP() <-> R_G() r9_k9f,r9_k9b
   # EGFR + Grb2 + SOS
  v10: R_G() + SOS() <-> R_GS() r10_k10f,r10_k10b
  v11: R_GS() <-> RP() + G_S()    r11_k11f,r11_k11b
   # SOS + Grb2
  v12: G_S() <-> Grb2() + SOS()  r12_k12f, r12_k12b
   # EGFR + Shc
  v13: Shc() + RP() <-> R_Sh() r13_k13f,r13_k13b
  # Shc Phosphorylation
  v14: R_Sh() <-> R_ShP() r14_k14f,r14_k14b
  # RShcP Breaking apart
  v15: R_ShP() <-> RP() + ShP() r15_k15f,r15_k15b
  # Shc Dephosphorylation
  v16: ShP() -> Shc() fRate15
  #  RShcP + Grb2
  v17: R_ShP() + Grb2() <-> R_Sh_G() r17_k17f,r17_k17b
  # RShG Breaking apart
  v18: R_Sh_G() <-> RP() + Sh_G()  r18_k18f,r18_k18b
  # RShG + SOS
  v19: R_Sh_G() + SOS() <-> R_Sh_GS()  r19_k19f,r19_k19b
  # RShGS Breaking apart
  v20: R_Sh_GS() <-> RP() + Sh_GS()    r20_k20f,r20_k20b
  # ShcP + Grb2
  v21: ShP() + Grb2() <-> Sh_G()   r21_k21f,r21_k21b
  # ShG + SOS
  v22: Sh_G() + SOS() <-> Sh_GS()  r22_k22f,r22_k22b
  # ShGS Breaking apart
  v23: Sh_GS() <-> ShP() + G_S()   r23_k23f,r23_k23b
  # RShP + GS
  v24: R_ShP() + G_S() <-> R_Sh_GS()  r24_k24f,r24_k24b
end reaction rules

end model

simulate({method=>"ode",t_end=>120,n_steps=>120})
