#This model is inferred from kholodenko model (biomodel)
begin model
begin parameters
#estimated parameters in github
r1_k1f 0.0183
r1_k1b 10
r2_k2f 10
r2_k2b 3.27
r3_k3f 2.32
r3_k3b 10
r4_V4 1000
r4_K4 21.15
r5_k5f 0.76
r5_k5b 5
r6_k6f 5
r6_k6b 2.07
r7_k7f 2.65
r7_k7b 10
r8_V8 1e-5
r8_K8 87.2
r9_k9f 0.22
r9_k9b 10
r10_k10f 5
r10_k10b 2.26
r11_k11f 1e-5
r11_k11b 1e-5
r12_k12f 1e-5
r12_k12b 0.00024
r13_k13f 0.057
r13_k13b 10
r14_k14f 10
r14_k14b 5
r15_k15f 1e-5
r15_k15b 1
r16_V16 5
r16_K16 0.0193
r17_k17f 1.49
r17_k17b 10
r18_k18f 5
r18_k18b 1e-5
r19_k19f 5
r19_k19b 1e-5
r20_k20f 3.34
r20_k20b 5
r21_k21f 10
r21_k21b 2.2
r22_k22f 5
r22_k22b 1e-5
r23_k23f 0.037
r23_k23b 1e-5
r24_k24f 1e-5
r24_k24b 0.048
r25_k25f 1.51
r25_k25b 0.0048
  
end parameters

begin molecule types
  EGF(egfr)
  EGFR(egf,egfr,p~P~0,shc,grb2,plcg)
  Shc(egfr,p~P~0,grb2)
  Grb2(egfr,sos,shc)
  SOS(grb2)
  PLCg(egfr,p~0~P,p_i~0~I)
end molecule types

begin seed species
  EGF(egfr)                   680 #nM
  EGFR(egf,egfr,p~0,shc,grb2,plcg) 100.0 #nM
  Shc(egfr,p~0,grb2)          150.0 #nM
  Grb2(egfr,sos,shc)          85 #nM
  SOS(grb2)                   34 #nM
  PLCg(egfr,p~0,p_i~0)        105 #nM
end seed species

begin observables
  Species EGF_EGFR2_P     EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1)    #RP in Kholodenko's paper
  Species Shc_P           Shc(egfr,p~P,grb2)                                #ShP in Kholodenko's paper
  Species PLCg_P          PLCg(egfr,p~P,p_i~0)
  Molecules EGFRPtotal    EGFR(p~P),EGFR(p~P)
  Molecules ShPtotal      Shc(p~P) 
  Molecules Grb2EGFRtotal Grb2(egfr!+), EGFR(shc!1).Shc(egfr!1,grb2!2).Grb2(shc!2)     
  Molecules Grb2Shctotal  Grb2(shc!+)    
  Molecules PLCgPtotal    PLCg(p~P,p_i~0)
end observables   

begin functions                                              
  fRate3() =  r4_V4/(EGF_EGFR2_P + r4_K4)
  fRate15() = r16_V16/(Shc_P + r16_K16)
  frate7() =  r8_V8 /(PLCg_P+r8_K8)
end functions

begin reaction rules
  # Ligand binding
  v1:  EGFR(egfr,egf,p~0,shc,grb2,plcg) + EGF(egfr) <-> EGFR(egfr,egf!1,p~0,shc,grb2,plcg).EGF(egfr!1) r1_k1f,r1_k1b
  # Dimerization
  v2:  EGFR(egfr,egf!1,p~0,shc,grb2,plcg).EGF(egfr!1) + EGFR(egfr,egf!1,p~0,shc,grb2,plcg).EGF(egfr!1) <-> EGFR(egfr!1,egf,p~0,shc,grb2,plcg).EGFR(egfr!1,egf,p~0,shc,grb2,plcg) 2*r2_k2f,0.5*r2_k2b
  # Phosphorylation
  v3:  EGFR(egfr!1,egf,p~0,shc,grb2,plcg).EGFR(egfr!1,egf,p~0,shc,grb2,plcg) <-> EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) 0.5*r3_k3f,r3_k3b
  # Dephosphorylation
  v4:  EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) -> EGFR(egfr!1,egf,p~0,shc,grb2,plcg).EGFR(egfr!1,egf,p~0,shc,grb2,plcg) fRate3
  # EGFR + PLCg
  v5:  EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) + PLCg(egfr,p~0,p_i~0) <-> EGFR(egfr,egf!1,p~P,shc,grb2,plcg!2).EGF(egfr!1).PLCg(egfr!2,p~0,p_i~0)    r5_k5f,r5_k5b
  # PLCg Phosphorylation
  v6:  EGFR(egfr,egf!1,p~P,shc,grb2,plcg!2).EGF(egfr!1).PLCg(egfr!2,p~0,p_i~0) <-> EGFR(egfr,egf!1,p~P,shc,grb2,plcg!2).EGF(egfr!1).PLCg(egfr!2,p~P,p_i~0)  r6_k6f,r6_k6b
  # RPLP Breaking apart
  v7:  EGFR(egfr,egf!1,p~P,shc,grb2,plcg!2).EGF(egfr!1).PLCg(egfr!2,p~P,p_i~0) <-> EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) + PLCg(egfr,p~P,p_i~0)    r7_k7f,r7_k7b
  # PLP Dephosphorylation
  v8:  PLCg(egfr,p~P,p_i~0) -> PLCg(egfr,p~0,p_i~0) frate7
  # EGFR + Grb2
  v9:  Grb2(egfr,sos,shc) + EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc,grb2!2,plcg).Grb2(egfr!2,sos,shc).EGF(egfr!1)  r9_k9f,r9_k9b
  # EGFRGrb2 + SOS
  v10: EGFR(egfr,egf!1,p~P,shc,grb2!2,plcg).Grb2(egfr!2,sos,shc).EGF(egfr!1) + SOS(grb2) <-> EGFR(egfr,egf!1,p~P,shc,grb2!2,plcg).Grb2(egfr!2,sos!3,shc).EGF(egfr!1).SOS(grb2!3)  r10_k10f,r10_k10b
  v11: EGFR(egfr,egf!1,p~P,shc,grb2!2,plcg).Grb2(egfr!2,sos!3,shc).EGF(egfr!1).SOS(grb2!3) <-> EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) + Grb2(egfr,sos!3,shc).SOS(grb2!3)  r11_k11f,r11_k11b
  # SOS + Grb2
  v12: Grb2(egfr,sos!3,shc).SOS(grb2!3) <-> Grb2(egfr,sos,shc) + SOS(grb2)  r12_k12f,r12_k12b
  # EGFR + Shc
  v13: Shc(egfr,p~0,grb2) + EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~0,grb2).EGF(egfr!1) r13_k13f,r13_k13b
  # Shc Phosphorylation
  v14: EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~0,grb2).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~P,grb2).EGF(egfr!1) r14_k14f,r14_k14b
  # RShcP Breaking apart
  v15: EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~P,grb2).EGF(egfr!1) <-> EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) + Shc(egfr,p~P,grb2) r15_k15f,r15_k15b
  # Shc Dephosphorylation
  v16: Shc(egfr,p~P,grb2) -> Shc(egfr,p~0,grb2) fRate15
  # RShcP + Grb2
  v17: EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~P,grb2).EGF(egfr!1) + Grb2(egfr,sos,shc) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~P,grb2!3).EGF(egfr!1).Grb2(egfr,sos,shc!3)  r17_k17f, r17_k17b
  # RShG Breaking apart
  v18: EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~P,grb2!3).EGF(egfr!1).Grb2(egfr,sos,shc!3) <-> EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) + Shc(egfr,p~P,grb2!3).Grb2(egfr,sos,shc!3)  r18_k18f,r18_k18b
  # RShG + SOS
  v19: EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~P,grb2!3).EGF(egfr!1).Grb2(egfr,sos,shc!3) + SOS(grb2) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~P,grb2!3).EGF(egfr!1).Grb2(egfr,sos!4,shc!3).SOS(grb2!4)  r19_k19f,r19_k19b
  # RShGS Breaking apart
  v20: EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~P,grb2!3).EGF(egfr!1).Grb2(egfr,sos!4,shc!3).SOS(grb2!4) <-> EGFR(egfr,egf!1,p~P,shc,grb2,plcg).EGF(egfr!1) + Shc(egfr,p~P,grb2!3).Grb2(egfr,sos!4,shc!3).SOS(grb2!4)   r20_k20f,r20_k20b
  # ShcP + Grb2
  v21: Shc(egfr,p~P,grb2) + Grb2(egfr,sos,shc) <-> Shc(egfr,p~P,grb2!1).Grb2(egfr,sos,shc!1)    r21_k21f,r21_k21b
  # ShG + SOS
  v22: Shc(egfr,p~P,grb2!1).Grb2(egfr,sos,shc!1) + SOS(grb2) <-> Shc(egfr,p~P,grb2!1).Grb2(egfr,sos!2,shc!1).SOS(grb2!2)  r22_k22f,r22_k22b
  # ShGS Breaking apart
  v23: Shc(egfr,p~P,grb2!1).Grb2(egfr,sos!2,shc!1).SOS(grb2!2) <-> Shc(egfr,p~P,grb2) + Grb2(egfr,sos!2,shc).SOS(grb2!2)   r23_k23f,r23_k23b
  # RShP + GS
  v24: EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~P,grb2).EGF(egfr!1) + Grb2(egfr,sos!4,shc).SOS(grb2!4) <-> EGFR(egfr,egf!1,p~P,shc!2,grb2,plcg).Shc(egfr!2,p~P,grb2!3).EGF(egfr!1).Grb2(egfr,sos!4,shc!3).SOS(grb2!4)   r24_k24f,r24_k24b
  # PLCg inactivation
  v25: PLCg(egfr,p~P,p_i~0) <-> PLCg(egfr,p~P,p_i~I)  r25_k25f,r25_k25b
end reaction rules

end model


generate_network({overwrite=>1})
writeSBML()
simulate({method=>"ode",t_end=>120,n_steps=>120})