#This model is the energy-based model of the EGFR signalling pathway to recover the Kholodenko;s model output
#In Step1, the model includes EGF, EGFR, and Shc
begin model
begin parameters
  #EGF,EGFR Parameters 
  r3_k3f   0.37     #1/s 
  r3_k3b   4.5      #1/s
  # Shc Parameters
  r14_k14f 0.18     #1/s
  r14_k14b 3.31e-3  #1/s  
  #Free Energies, J/nM
  Gf_LR    -8             
  Gf_RR    -1.86
  Gf_RSh   -5.6 
  #Cooperative terms
  Gf_LRR   -10
  Gf_LRRL  1.03
  Gf_RShP  12  
  #rate distribution parameter, no units
  phi      0.35
  #Activation Energies, J/nM
  Ea_LR    10
  Ea_RR    3.72
  Ea_RSh   4.7
end parameters

begin molecule types
  EGF(egfr)
  EGF0(egfr)
  EGFR0(egf,egfr)
  EGFR(egf,egfr,shc~0~P)
  Shc(egfr,grb2~0~P)
end molecule types

begin seed species
  EGF(egfr)                    680.0  #nM
  EGF0(egfr)                   680.0  #nM
  EGFR0(egf,egfr)              100.0  #nM
  EGFR(egf,egfr,shc~0)         100.0  #nM
  Shc(egfr,grb2~0)             150.0  #nM
end seed species

begin energy patterns
  EGFR(egf!0).EGF(egfr!0)                                        Gf_LR
  EGFR0(egf!0).EGF0(egfr!0)                                      Gf_LR
  EGFR(egfr!1).EGFR0(egfr!1)                                     Gf_RR+ 2*Gf_LRR + Gf_LRRL
  EGFR(shc~P!2).Shc(egfr!2)                                      Gf_RSh
  EGFR(shc~P!2).Shc(egfr!2,grb2~P)                               Gf_RShP
end energy patterns

begin observables
  Molecules Dimers          EGFR(egfr!+)
  Species   EGF_EGFR2_P     EGFR(shc~P)        
  Species   Shc_P           Shc(egfr,grb2~P!?) 
  Molecules EGFRPtotal      EGFR(shc~P!?)
  Molecules ShPtotal        Shc(grb2~P!?) 
end observables                   

begin reaction rules
    # Ligand-receptor binding 
  v1_1: EGFR(egfr,egf,shc) + EGF(egfr) <-> EGFR(egfr,egf!1,shc).EGF(egfr!1) Arrhenius(phi,Ea_LR) 
  v1_2: EGFR0(egfr,egf) + EGF0(egfr) <-> EGFR0(egfr,egf!1).EGF0(egfr!1) Arrhenius(phi,Ea_LR)  
    # Receptor-aggregation (JIM: This rule is restrictive because only unphosphorylated receptors can undimerize.)
  v2: EGFR(egfr,egf!+,shc~0) + EGFR0(egfr,egf!+) <-> EGFR(egfr!1,egf!+,shc~0).EGFR0(egfr!1,egf!+) Arrhenius(phi,Ea_RR)
    # Transphosphorylation of Egfr in dimer (bound at egfr)
  v3: EGFR(egfr!1,shc~0).EGFR0(egfr!1) -> EGFR(egfr!1,shc~P).EGFR0(egfr!1) r3_k3f
    # Dephosphorylation by phosphotase 
  v4: EGFR(shc~P) -> EGFR(shc~0) r3_k3b
    # Egfr + Shc
  v13: Shc(egfr) + EGFR(shc~P) <-> EGFR(shc~P!1).Shc(egfr!1) Arrhenius(phi,Ea_RSh) 
    # Shc transphos
  v14: EGFR0(egfr!2).EGFR(egfr!2,shc~P!1).Shc(egfr!1,grb2~0) -> EGFR0(egfr!2).EGFR(egfr!2,shc~P!1).Shc(egfr!1,grb2~P)  r14_k14f
    # Dephosphorylayion of Shc by phosphotase
  v16: Shc(egfr,grb2~P) -> Shc(egfr,grb2~0) r14_k14b
end reaction rules

end model

generate_network({overwrite=>1})
simulate({method=>"ode",t_end=>120,n_steps=>1200})
writeSBML()


