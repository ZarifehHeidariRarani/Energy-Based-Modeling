#This model is the energy-based model of the EGFR signalling pathway to recover the Kholodenko;s model output
#In Step2, the model includes EGF, EGFR, Shc, Grb2, SOS, and PLCg
begin model
begin parameters
 #EGF,EGFR Parameters 
  k3f1   0.37  #1/s #Phosphorylation for Shc site 
  k3b1   4.5   #1/s #De-Phosphorylation for Shc site
  k3f2   0.07  #1/s #Phosphorylation for Grb2 site 
  k3b2   1     #1/s #De-Phosphorylation for Grb2 site 
  k3f3   0.04  #1/s #Phosphorylation for PLCg site 
  k3b3   7     #1/s #De-Phosphorylation for PLCg site 
  # Shc Parameters
  r14_k14f 0.18    #1/s
  r14_k14b 3.31e-3 #1/s  
  # PLCg Parameteres 
  r6_k6f   0.3     #1/s
  r6_k6b   0.0014  #1/s
  r25_k25f 0.18    #1/s
  r25_k25b 0.006   #1/s
  #Free Energies, J/nM
  Gf_LR    -8             
  Gf_RR    -1.86
  Gf_RSh   -5.6 
  Gf_RG    2.5
  Gf_GS    1
  Gf_ShG   1
  Gf_RPL   1.5
  #Cooperative terms
  Gf_LRR   -10
  Gf_LRRL  1.03
  Gf_RShP  12  
  Gf_RGS   0.1
  Gf_RShG  15
  Gf_ShGS  5
  Gf_RPLP  6
  #Multiple Bindings
  Gf_RPLCGrb  1
  Gf_RPLCShc  1
  Gf_RShcGrb  1
  #rate distribution parameter, no units
  phi      0.35
  #Activation Energies, J/nM
  Ea_LR    10
  Ea_RR    3.72
  Ea_RSh   4.7
  Ea_GS    1
  Ea_RG    5
  Ea_RPL   -6
  Ea_ShG   -3 
end parameters

begin molecule types
  EGF0(egfr)
  EGFR0(egf,egfr)
  EGF(egfr)
  EGFR(egf,egfr,shc~0~P,grb2~0~P,plcg~0~P)
  Shc(egfr,grb2~0~P)
  Grb2(SH2,sos)
  SOS(grb2)
  PLCg(egfr,s~0~P,p_i~0~I)
end molecule types

begin seed species
  EGF0(egfr)                   680.0         #nM
  EGFR0(egf,egfr)              100.0         #nM
  EGF(egfr)                    680.0         #nM
  EGFR(egf,egfr,shc~0,grb2~0,plcg~0)  100.0  #nM
  Shc(egfr,grb2~0)             150.0         #nM
  Grb2(SH2,sos)                85.0          #nM
  SOS(grb2)                    34.0          #nM
  PLCg(egfr,s~0,p_i~0)         105.0         #nM  
end seed species

begin energy patterns
  EGFR(egf!0).EGF(egfr!0)                                        Gf_LR
  EGFR0(egf!0).EGF0(egfr!0)                                      Gf_LR
  EGFR(egfr!1).EGFR(egfr!1)                                      Gf_RR+ 2*Gf_LRR + Gf_LRRL
  EGFR(shc~P!2).Shc(egfr!2)                                      Gf_RSh
  EGFR(shc~P!2).Shc(egfr!2,grb2~P)                               Gf_RShP
  EGFR(grb2~P!2).Grb2(SH2!2)                                     Gf_RG
  Grb2(sos!1).SOS(grb2!1)                                        Gf_GS
  EGFR(grb2~P!2).Grb2(SH2!2,sos!1).SOS(grb2!1)                   Gf_RGS
  Grb2(SH2!1).Shc(grb2~P!1)                                      Gf_ShG
  EGFR(shc~P!2).Shc(egfr!2,grb2~P!1).Grb2(SH2!1)                 Gf_RShG
  Shc(grb2~P!1).Grb2(SH2!1,sos!2).SOS(grb2!2)                    Gf_ShGS
  EGFR(plcg~P!1).PLCg(egfr!1)                                    Gf_RPL
  EGFR(plcg~P!1).PLCg(egfr!1,s~P)                                Gf_RPLP
  PLCg(egfr!0).EGFR(plcg~P!0,grb2~P!1).Grb2(SH2!1)               Gf_RPLCGrb
  PLCg(egfr!0).EGFR(plcg~P!0,shc~P!1).Shc(egfr!1)                Gf_RPLCShc
  Grb2(SH2!0).EGFR(grb2~P!0,shc~P!1).Shc(egfr!1)                 Gf_RShcGrb
end energy patterns

begin observables
  Molecules EGFRPtotal      EGFR(shc~P!?), EGFR(grb2~P!?), EGFR(plcg~P!?)
  Molecules ShPtotal        Shc(grb2~P!?) 
  Molecules Grb2EGFRtotal   Grb2(SH2!0).EGFR(grb2~P!0), Grb2(SH2!1).Shc(egfr!0,grb2~P!1).EGFR(shc~P!0)
  Molecules Grb2Shctotal    Grb2(SH2!0).Shc(grb2~P!0), Grb2(SH2!0).EGFR(grb2~P!0,shc~P!1).Shc(egfr!1)
  Molecules PLCgPtotal      PLCg(s~P,p_i~0)
end observables                   

begin reaction rules
    # Ligand-receptor binding 
  v1_1: EGFR(egfr,egf,shc,grb2,plcg) + EGF(egfr) <-> EGFR(egfr,egf!1,shc,grb2,plcg).EGF(egfr!1) Arrhenius(phi,Ea_LR) 
  v1_2: EGFR0(egfr,egf) + EGF0(egfr) <-> EGFR0(egfr,egf!1).EGF0(egfr!1) Arrhenius(phi,Ea_LR)   
    # Receptor-aggregation (JIM: This rule is restrictive because only unphosphorylated receptors can undimerize.)
 #v2: EGFR(egfr,egf!+,shc~0,grb2~0,plcg~0) + EGFR0(egfr,egf!+) <-> EGFR(egfr!1,egf!+,shc~0,grb2~0,plcg~0).EGFR0(egfr!1,egf!+) Arrhenius(phi,Ea_RR)
    #Now both phosphorylated and unphosphorylated receptors can undimerize
  v2: EGFR(egfr,egf!+) + EGFR0(egfr,egf!+) <-> EGFR(egfr!1,egf!+).EGFR0(egfr!1,egf!+) Arrhenius(phi,Ea_RR)  
    # Transphosphorylation of Egfr in dimer (bound at egfr)
  v3_1: EGFR(egfr!1,shc~0).EGFR0(egfr!1) -> EGFR(egfr!1,shc~P).EGFR0(egfr!1)    k3f1
  v3_2: EGFR(egfr!1,grb2~0).EGFR0(egfr!1) -> EGFR(egfr!1,grb2~P).EGFR0(egfr!1)  k3f2
  v3_3: EGFR(egfr!1,plcg~0).EGFR0(egfr!1)  -> EGFR(egfr!1,plcg~P).EGFR0(egfr!1) k3f3
    # Dephosphorylation by phosphotase 
  v4_1: EGFR(shc~P) -> EGFR(shc~0)   k3b1
  v4_2: EGFR(grb2~P) -> EGFR(grb2~0) k3b2
  v4_3: EGFR(plcg~P) -> EGFR(plcg~0) k3b3
    # Egfr + Shc 
  v13: Shc(egfr) + EGFR(shc~P) <-> EGFR(shc~P!1).Shc(egfr!1) Arrhenius(phi,Ea_RSh) 
    # Shc transphos
  v14: EGFR0(egfr!2).EGFR(egfr!2,shc~P!1).Shc(egfr!1,grb2~0) -> EGFR0(egfr!2).EGFR(egfr!2,shc~P!1).Shc(egfr!1,grb2~P)  r14_k14f
    # Dephosphorylayion of Shc by phosphotase 
  v16: Shc(egfr,grb2~P) -> Shc(egfr,grb2~0) r14_k14b
    # Egfr + Grb2
  v9:  Grb2(SH2) + EGFR(grb2~P) <-> EGFR(grb2~P!1).Grb2(SH2!1)  Arrhenius(phi,Ea_RG) 
    # Grb2 + SOS
  v12: Grb2(sos) + SOS(grb2) <->  Grb2(sos!1).SOS(grb2!1) Arrhenius(phi,Ea_GS)
    # Grb2 + Shc
  v21: Grb2(SH2) + Shc(grb2~P) <->  Grb2(SH2!1).Shc(grb2~P!1) Arrhenius(phi,Ea_ShG)
    # Egfr + PLCg (Don't allow inactive form to bind)
  v5:  EGFR(plcg~P) + PLCg(egfr,p_i~0)  <->  EGFR(plcg~P!1).PLCg(egfr!1,p_i~0)        Arrhenius(phi,Ea_RPL)  
    # PLCg transphos
  v6: EGFR0(egfr!2).EGFR(egfr!2,plcg~P!1).PLCg(egfr!1,s~0,p_i~0) -> EGFR0(egfr!2).EGFR(egfr!2,plcg~P!1).PLCg(egfr!1,s~P,p_i~0)   r6_k6f
    # PLCg dephosphorylation, catalytic
  v8: PLCg(egfr,s~P,p_i~0) -> PLCg(egfr,s~0,p_i~0) r6_k6b
    # PLCg inactivation
  v25: PLCg(egfr,s~P,p_i~0)  <->  PLCg(egfr,s~P,p_i~I)  r25_k25f, r25_k25b
end reaction rules

end model

generate_network({overwrite=>1})
simulate({method=>"ode",t_end=>120,n_steps=>120})
writeSBML()