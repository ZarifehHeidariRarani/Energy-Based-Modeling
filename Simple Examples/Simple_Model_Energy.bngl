begin model

begin parameters
  kp     0.1
  kd     0.01
  #Free Energies, J/nM
  Gf_RA   1
  Gf_RB   1
  Gf_AB   1
  #Cooperative terms
  Gf_RAp  1
  Gf_RApB 1
  #
  phi     0.5
  #Activation Energies, J/nM
  Ea_RA   1
  Ea_RB   1
  Ea_AB   1
end parameters

begin molecule types
  R(a,b)
  A(r,Y~0~P)
  B(s)
end molecule types

begin species
  R(a,b)      1
  A(r,Y~0)    1
  B(s)        1
end species

begin energy patterns
  R(a!0).A(r!0)                   Gf_RA
  R(a!0).A(r!0,Y~P)               Gf_RAp
  R(b!1).B(s!1)                   Gf_RB
  A(Y~P!1).B(s!1)                 Gf_AB
  R(a!0).A(r!0,Y~P!1).B(s!1)      Gf_RApB
end energy patterns 

begin observables
  Molecules Abound   R(a!1).A(r!1)
  Molecules Abound1  A(r!+)
  Molecules Ap       A(Y~P!?)
  Molecules BboundR  R(b!2).B(s!2)
  Molecules BboundA  A(Y~P!1).B(s!1), R(a!1,b).A(r!1,Y~P!2).B(s!2)
end observables

begin reaction rules
ARbind:  A(r) + R(a) <-> R(a!0).A(r!0)            Arrhenius(phi,Ea_RA) 
Aphos:   R(a!1).A(r!1,Y~0) -> R(a!1).A(r!1,Y~P)   kp
Adephos: A(r,Y~P) -> A(r,Y~0)                     kd
BRbind:  B(s) + R(b) <-> R(b!2).B(s!2)            Arrhenius(phi,Ea_RB) 
ABbind:  A(Y~P) + B(s) <-> A(Y~P!1).B(s!1)        Arrhenius(phi,Ea_AB) 
end reaction rules

end model
generate_network({overwrite=>1})
simulate({method=>"ode",t_end=>10,n_steps=>20})