begin model

begin parameters
  k1p    1
  k1m    0.1
  k2p    1
  k2m    0.1
  kd     0.2
  k3p    0.01
  k3m    0.001
  k4p    1
  k4m    0.3
  k5p    1 
  k5m    0.1
  k6p    1
  k6m    0.1
  k7p    1
  # There is a detaile balance Constraint on the loop of K3-K5-K7-K6 
  # It leads to (k3p*k5p*k6m*k7m / k3m*k5m*k6p*k7p) =1
  k7m   (k3m*k5m*k6p*k7p)/(k3p*k5p*k6m) 
end parameters

begin molecule types
  R(a,b)
  A(r,Y~0~P)
  B(s)
end molecule types

begin species
  R(a,b)      1
  A(r,Y~0)    1
  B(s)        1
end species

begin observables
  Molecules AboundR   A(r!+)
  Molecules BboundR   R(b!1).B(s!1)
  Molecules Ap        A(Y~P!?)
  Molecules BboundA   A(Y~P!1).B(s!1), R(a!1,b).A(r!1,Y~P!2).B(s!2)
end observables

begin reaction rules
AbindR:   R(a,b) + A(r,Y~0)  <-> R(a!1,b).A(r!1,Y~0)      k1p, k1m
Aphos:    R(a!1,b).A(r!1,Y~0) <-> R(a!1,b).A(r!1,Y~P)     k2p, k2m
Adephos:  A(r,Y~P) -> A(r,Y~0)                            kd
ApbindR:  R(a,b) + A(r,Y~P)  <-> R(a!1,b).A(r!1,Y~P)      k3p, k3m
BbindR:   R(a,b) + B(s) <-> R(a,b!1).B(s!1)               k4p, k4m
RApbindB: R(a!1,b).A(r!1,Y~P) + B(s) <-> R(a!1,b).A(r!1,Y~P!2).B(s!2)  k5p, k5m
ApbindB:  A(r,Y~P) + B(s) <-> A(r,Y~P!2).B(s!2)           k6p, k6m
ApBbindR: A(r,Y~P!2).B(s!2) + R(a,b) <-> R(a!1,b).A(r!1,Y~P!2).B(s!2)  k7p, k7m
end reaction rules

end model

simulate({method=>"ode",t_end=>200,n_steps=>200})